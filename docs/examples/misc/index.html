<!DOCTYPE html>
<html lang="en">

<head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    
    
    
    
    <link rel="shortcut icon" href="../../img/favicon.ico">
    <title> misc  - Faust Documentation
    </title>
    <link href="../../css/bootstrap.min.css" rel="stylesheet">
    <link href="../../css/font-awesome.min.css" rel="stylesheet">
    <link href="../../css/base.css" rel="stylesheet">
    <link href="/css/quickref.css" rel="stylesheet">
    <link href="/css/github.min.css" rel="stylesheet">
    <link href="/rail/railroad-diagrams.css" rel="stylesheet">
    <script src="../../js/jquery-3.6.0.min.js" defer></script>
    <script src="../../js/bootstrap.min.js" defer></script>
    <script src="../../js/MathJax-2.7.1/MathJax.js?config=TeX-AMS-MML_HTMLorMML"defer></script>
    <script src="../../js/highlight.min.js"></script>
    <script>hljs.initHighlightingOnLoad();</script>
</head>

<body>
    <div class="navbar fixed-top navbar-expand-lg navbar-dark bg-primary">
        <div class="container">
            <a class="navbar-brand" href="../.."><img
                    src="../../img/faustText.svg" width="150px"> </a>
            <!-- Expander button -->
            <button type="button" class="navbar-toggler" data-toggle="collapse" data-target="#navbar-collapse">
                <span class="navbar-toggler-icon"></span>
            </button>

            <!-- Expanded navigation -->
            <div id="navbar-collapse" class="navbar-collapse collapse">
                <!-- Main navigation -->
                <ul class="nav navbar-nav">
                    <li class="navitem">
                        <a href="../.." class="nav-link">Home</a>
                    </li>
                    <li class="dropdown">
                        <a href="#" class="nav-link dropdown-toggle" data-toggle="dropdown">Manual <b
                                class="caret"></b></a>
                        <ul class="dropdown-menu">
                            
<li>
    <a href="../../manual/introduction/" class="dropdown-item">Introduction</a>
</li>
                            
<li>
    <a href="../../manual/overview/" class="dropdown-item">Overview of the Faust Universe</a>
</li>
                            
<li>
    <a href="../../manual/quick-start/" class="dropdown-item">Quick Start</a>
</li>
                            
<li>
    <a href="../../manual/syntax/" class="dropdown-item">Faust Syntax</a>
</li>
                            
<li>
    <a href="../../manual/errors/" class="dropdown-item">Error Messages</a>
</li>
                            
<li>
    <a href="../../manual/compiler/" class="dropdown-item">Using the Compiler</a>
</li>
                            
<li>
    <a href="../../manual/options/" class="dropdown-item">Compiler Options</a>
</li>
                            
<li>
    <a href="../../manual/tools/" class="dropdown-item">faust2[...] Tools</a>
</li>
                            
<li>
    <a href="../../manual/architectures/" class="dropdown-item">Architecture Files</a>
</li>
                            
<li>
    <a href="../../manual/embedding/" class="dropdown-item">Embedding the Compiler</a>
</li>
                            
<li>
    <a href="../../manual/optimizing/" class="dropdown-item">Optimizing the Code</a>
</li>
                            
<li>
    <a href="../../manual/debugging/" class="dropdown-item">Debugging the Code</a>
</li>
                            
<li>
    <a href="../../manual/deploying/" class="dropdown-item">Deploying on the Web</a>
</li>
                            
<li>
    <a href="../../manual/remote/" class="dropdown-item">Remote Compilation</a>
</li>
                            
<li>
    <a href="../../manual/mathdoc/" class="dropdown-item">Mathematical Documentation</a>
</li>
                            
<li>
    <a href="../../manual/osc/" class="dropdown-item">OSC Support</a>
</li>
                            
<li>
    <a href="../../manual/http/" class="dropdown-item">HTTP Support</a>
</li>
                            
<li>
    <a href="../../manual/midi/" class="dropdown-item">MIDI Support</a>
</li>
                            
<li>
    <a href="../../manual/soundfiles/" class="dropdown-item">Soundfiles Support</a>
</li>
                            
<li>
    <a href="../../manual/community/" class="dropdown-item">Community</a>
</li>
                            
<li>
    <a href="../../manual/faq/" class="dropdown-item">Frequently Asked Questions</a>
</li>
                        </ul>
                    </li>
                    <li class="dropdown active">
                        <a href="#" class="nav-link dropdown-toggle" data-toggle="dropdown">Examples <b
                                class="caret"></b></a>
                        <ul class="dropdown-menu">
                            
<li>
    <a href=".." class="dropdown-item"> All examples </a>
</li>
                            
<li>
    <a href="../ambisonics/" class="dropdown-item"> ambisonics </a>
</li>
                            
<li>
    <a href="../analysis/" class="dropdown-item"> analysis </a>
</li>
                            
<li>
    <a href="../bela/" class="dropdown-item"> bela </a>
</li>
                            
<li>
    <a href="../delayEcho/" class="dropdown-item"> delayEcho </a>
</li>
                            
<li>
    <a href="../dynamic/" class="dropdown-item"> dynamic </a>
</li>
                            
<li>
    <a href="../filtering/" class="dropdown-item"> filtering </a>
</li>
                            
<li>
    <a href="../gameaudio/" class="dropdown-item"> gameaudio </a>
</li>
                            
<li>
    <a href="../generator/" class="dropdown-item"> generator </a>
</li>
                            
<li>
    <a href="./" class="dropdown-item active"> misc </a>
</li>
                            
<li>
    <a href="../phasing/" class="dropdown-item"> phasing </a>
</li>
                            
<li>
    <a href="../physicalModeling/" class="dropdown-item"> physicalModeling </a>
</li>
                            
<li>
    <a href="../pitchShifting/" class="dropdown-item"> pitchShifting </a>
</li>
                            
<li>
    <a href="../psychoacoustic/" class="dropdown-item"> psychoacoustic </a>
</li>
                            
<li>
    <a href="../reverb/" class="dropdown-item"> reverb </a>
</li>
                            
<li>
    <a href="../smartKeyboard/" class="dropdown-item"> smartKeyboard </a>
</li>
                            
<li>
    <a href="../spat/" class="dropdown-item"> spat </a>
</li>
                            
<li>
    <a href="../../rsrc/examples.zip" class="dropdown-item"> Download examples </a>
</li>
                        </ul>
                    </li>
                    <li class="dropdown">
                        <a href="#" class="nav-link dropdown-toggle" data-toggle="dropdown">Tutorials <b
                                class="caret"></b></a>
                        <ul class="dropdown-menu">
                            
<li>
    <a href="../../workshops/2020-04-10-faust-juce/" class="dropdown-item">Faust & JUCE</a>
</li>
                            
<li>
    <a href="../../tutorials/teensy/" class="dropdown-item">DSP on the Teensy With Faust</a>
</li>
                            
<li>
    <a href="../../tutorials/esp32/" class="dropdown-item">DSP on the ESP-32 With Faust</a>
</li>
                            
<li>
    <a href="../../tutorials/basic-osc/" class="dropdown-item">Making a Sine Oscillator From Scratch</a>
</li>
                            
<li>
    <a href="../../tutorials/summation/" class="dropdown-item">RMS and Summation in Faust</a>
</li>
                            
<li>
    <a href="../../tutorials/box-api/" class="dropdown-item">Using the box API</a>
</li>
                            
<li>
    <a href="../../tutorials/signal-api/" class="dropdown-item">Using the signal API</a>
</li>
                            
<li>
    <a href="../../tutorials/julia/" class="dropdown-item">Using Faust in Julia</a>
</li>
                            
<li>
    <a href="../../tutorials/cmajor/" class="dropdown-item">Using Faust in Cmajor</a>
</li>
                            
<li>
    <a href="../../tutorials/jsfx/" class="dropdown-item">Using Faust in JSFX</a>
</li>
                            
<li>
    <a href="../../tutorials/rnbo/" class="dropdown-item">Using Faust in RNBO with codebox~</a>
</li>
                            
<li>
    <a href="../../tutorials/debugging/" class="dropdown-item">Advanced debugging with interp-tracer</a>
</li>
                        </ul>
                    </li>
                    <li class="dropdown">
                        <a href="#" class="nav-link dropdown-toggle" data-toggle="dropdown">Workshops <b
                                class="caret"></b></a>
                        <ul class="dropdown-menu">
                            
<li>
    <a href="../../workshops/2018-12-01-paw/" class="dropdown-item"> 2018-12-01 PAW </a>
</li>
                            
<li>
    <a href="../../workshops/2020-03-24-faust-citi/" class="dropdown-item"> 2020-03-24 CITI </a>
</li>
                            
<li>
    <a href="../../workshops/2020-04-10-faust-101/" class="dropdown-item"> 2020-04-10 Faust 101 </a>
</li>
                            
<li>
    <a href="../../workshops/2020-04-10-faust-juce/" class="dropdown-item"> 2020-04-10 Faust & JUCE </a>
</li>
                            
<li>
    <a href="../../workshops/2020-11-21-faust-vcvrack/" class="dropdown-item"> 2020-11-21 Faust & VCV Rack </a>
</li>
                        </ul>
                    </li>
                    <li class="navitem">
                        <a href="../../about/" class="nav-link">About</a>
                    </li>
                </ul>

                <ul class="nav navbar-nav ml-auto">
                    <li class="nav-item">
                        <a href="#" class="nav-link" data-toggle="modal" data-target="#mkdocs_search_modal">
                            <i class="fa fa-search"></i> Search
                        </a>
                    </li>
                </ul>
            </div>
        </div>
    </div>

    

    <div class="container">
        <div class="row"><div class="col-md-3"><div class="navbar-light navbar-expand-md bs-sidebar hidden-print affix" role="complementary">
    <div class="navbar-header">
        <button type="button" class="navbar-toggler collapsed" data-toggle="collapse" data-target="#toc-collapse" title="Table of Contents">
            <span class="fa fa-angle-down"></span>
        </button>
    </div>

    
    <div id="toc-collapse" class="navbar-collapse collapse card bg-secondary">
        <ul class="nav flex-column">
            
            <li class="nav-item" data-level="1"><a href="#misc" class="nav-link">misc</a>
              <ul class="nav flex-column">
            <li class="nav-item" data-level="2"><a href="#uitester" class="nav-link">UITester</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#autopan" class="nav-link">autopan</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#capture" class="nav-link">capture</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#drumkit" class="nav-link">drumkit</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#inlooper" class="nav-link">inlooper</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#matrix" class="nav-link">matrix</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#miditester" class="nav-link">midiTester</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#paradigma" class="nav-link">paradigma</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#sflooper" class="nav-link">sflooper</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#statespace" class="nav-link">statespace</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#switcher" class="nav-link">switcher</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#tester" class="nav-link">tester</a>
              <ul class="nav flex-column">
              </ul>
            </li>
            <li class="nav-item" data-level="2"><a href="#tester2" class="nav-link">tester2</a>
              <ul class="nav flex-column">
              </ul>
            </li>
              </ul>
            </li>
        </ul>
    </div>
</div></div>
            <div class="col-md-9 main-container" role="main">

<h1 id="misc">misc</h1>
<h2 id="uitester">UITester</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust0/exfaust0.svg" class="mx-auto d-block">
<faust-editor><!--

declare name "UITester";
declare version "1.0";
declare author "O. Guillerminet";
declare license "BSD";
declare copyright "(c) O. Guillerminet 2012";

vbox = vgroup("vbox", 
    checkbox("check1"),
    checkbox("check2"),
     nentry("knob0[style:knob]", 60, 0, 127, 0.1));

sliders = hgroup("sliders",
        vslider("vslider1", 60, 0, 127, 0.1),
        vslider("vslider2", 60, 0, 127, 0.1),
        vslider("vslider3", 60, 0, 127, 0.1));

knobs = hgroup("knobs",
        vslider("knob1[style:knob]", 60, 0, 127, 0.1),
        vslider("knob2[style:knob]", 60, 0, 127, 0.1),
        vslider("knob3[style:knob]", 60, 0, 127, 0.1));

smallhbox1 = hgroup("small box 1",
            vslider("vslider5 [unit:Hz]", 60, 0, 127, 0.1),
            vslider("vslider6 [unit:Hz]", 60, 0, 127, 0.1),
            vslider("knob4[style:knob]", 60, 0, 127, 0.1),
            nentry("num1 [unit:f]", 60, 0, 127, 0.1),
            vbargraph("vbar1", 0, 127));

smallhbox2 = hgroup("small box 2",
            vslider("vslider7 [unit:Hz]", 60, 0, 127, 0.1),
            vslider("vslider8 [unit:Hz]", 60, 0, 127, 0.1),
            vslider("knob5[style:knob]", 60, 0, 127, 0.1),
            nentry("num2 [unit:f]", 60, 0, 127, 0.1),
            vbargraph("vbar2", 0, 127));

smallhbox3 = hgroup("small box 3",
            vslider("vslider9 [unit:Hz]", 60, 0, 127, 0.1),
            vslider("vslider10 [unit:m]", 60, 0, 127, 0.1),
            vslider("knob6[style:knob]", 60, 0, 127, 0.1),
            nentry("num3 [unit:f]", 60, 0, 127, 0.1),
            vbargraph("vbar3", 0, 127));

subhbox1 = hgroup("sub box 1",
        smallhbox2,
        smallhbox3);

vmisc = vgroup("vmisc",
        vslider("vslider4 [unit:Hz]", 60, 0, 127, 0.1),
        button("button"),
        hslider("hslider [unit:Hz]", 60, 0, 127, 0.1),
        smallhbox1,
        subhbox1,
        hbargraph("hbar", 0, 127));

hmisc = hgroup("hmisc",
        vslider("vslider4 [unit:f]", 60, 0, 127, 0.1),
        button("button"),
        hslider("hslider", 60, 0, 127, 0.1),
        nentry("num [unit:f]", 60, 0, 127, 0.1),
        vbargraph("vbar", 0, 127),
        hbargraph("hbar", 0, 127));

//------------------------- Process --------------------------------

process = tgroup("grp 1",
        vbox,
        sliders,
        knobs,
        vmisc,
        hmisc);


--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="autopan">autopan</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust1/exfaust1.svg" class="mx-auto d-block">
<faust-editor><!--

//--------------------------autopan----------------------------
//
// For a stereo input, adjust the left and right gains
// according to rate-synced sine oscillators. This example is
// reverse-engineered from Ableton Live's "Auto Pan" audio
// effect. We don't implement the "spin" feature in which
// the two LFOs could have different frequencies.
// We also don't use other LFOs such as triangle/square/noise.
//
// #### Usage
//
// 
// si.bus(2) : autopan(amount, rate, phase, shape) : si.bus(2)
// 
//
// Where:
//
// * amount: adjusts the amount of LFO modulation that is
//    applied to incoming signals
// * rate: sets the frequency of the LFO in Hertz
// * phase: adjusts the amount of the offset between the
//    waveforms for the left and right channel. At 180,
//    the LFOs will be perfectly out of phase. For other
//    values, you will hear more moments where the
//    overall volume is reduced
// * shape: turning shape up pushes the waveforms to their
//    upper and lower limits, "hardening" their shape
//------------------------------------------------------------
import("stdfaust.lib");

autopan(amount, rate, phase, shape) = _*gainLeft, _*gainRight
with {

    // A saturator
    // Assume x is a signal between -1 and 1. This function
    // pushes the output towards -1 and 1. As the shape
    // parameter goes from 0 to 1, an input sine will become
    // closer to a square. If shape is zero, then the
    // function doesn't change the input signal.
    saturator(shape, x) = result
    with {
        // It's ok to replace tanh with another saturator
        result = x, ma.tanh(x*10.) : it.interpolate_linear(shape);
    };

    phase2Gain(phase) = os.oscp(rate, phase)
        : saturator(shape) // comment out this line to remove the saturator
        : it.remap(-1., 1., 1.-amount, 1.);

    gainLeft = 0. : phase2Gain;
    gainRight = phase : ma.deg2rad : phase2Gain;
};

amount = hslider("[0]Amount[style:knob]", 0., 0., 1., .001);
rate = hslider("[1]Rate[style:knob][unit:Hz][scale:log]", 1., .05, 90., .001);
phase = hslider("[2]Phase[style:knob][unit:deg]", 180., 0., 360., 15) : si.smoo;
shape = hslider("[3]Shape[style:knob]", 0., 0., 1., 0.001) : si.smoo;

process = hgroup("Auto Pan", autopan(amount, rate, phase, shape));

--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="capture">capture</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust2/exfaust2.svg" class="mx-auto d-block">
<faust-editor><!--

declare name        "capture";
declare version     "1.0";
declare author      "Grame";
declare license     "BSD";
declare copyright   "(c)GRAME 2006";

//-------------------------------------------------
//       Capture : record up to 8s of sound and
//        playback the recorded sound in loop
//-------------------------------------------------

import("stdfaust.lib");

B = button("Capture");    // Capture sound while pressed
I = int(B);               // convert button signal from float to integer
R = (I-I') <= 0;          // Reset capture when button is pressed
D = (+(I):*(R))~_;        // Compute capture duration while button is pressed: 0..NNNN0..MMM

capture = *(B) : (+ : de.delay(8*65536, D-1)) ~ *(1.0-B);

level = hslider("level (db)", 0, -96, 4, 0.1) : ba.db2linear : si.smoo;

process = vgroup("Audio Capture", capture : *(level));


--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="drumkit">drumkit</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust3/exfaust3.svg" class="mx-auto d-block">
<faust-editor><!--


//-------------------------------------------------
//    A simple, fully generative
//    drum machine
//-------------------------------------------------

import("stdfaust.lib");

drumkit(gate, ks, ss, hs) = k_seq, s_seq, h_seq :> /(3)
    with {
        N = outputs(ks);
        env = en.ar(0.001, 0.05);
        kick(g) = g : env : *(os.osc(100));
        snare(g) = g : env : *(no.noise : fi.low_shelf(-60, 3000));
        hihat(g) = g : env : *(no.noise : fi.highpass(3, 10000));
        sequencer(t) = t : ba.selectn(N, gate : ba.pulse_countup_loop(N - 1, 1)) : *(gate : mem);
        k_seq = sequencer(ks) : kick;
        s_seq = sequencer(ss) : snare;
        h_seq = sequencer(hs) : hihat;
    };

drumkit_ui(n) = drumkit(attach(gate, nn), ks, ss, hs)
    with {
        gate = ba.pulse(ba.tempo(bpm));
        ks = par(i, n, checkbox("v:Drum kit/h:[0]kick/%2i"));
        ss = par(i, n, checkbox("v:Drum kit/h:[1]snare/%2i"));
        hs = par(i, n, checkbox("v:Drum kit/h:[2]hihat/%2i"));
        bpm = hslider("v:Drum kit/[4]BPM", 350, 10, 800, 1);
        mon = par(i, n, hbargraph("v:Drum kit/h:[3]seq/%2i[style:led]", 0, 1));
        nn = 1 : ba.selectoutn(n, gate : ba.pulse_countup_loop(n - 1, 1)) : mon :> _;
    };

drumkit_ui_demo = drumkit_ui(15) <: (_, _);

process = drumkit_ui_demo;


--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="inlooper">inlooper</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust4/exfaust4.svg" class="mx-auto d-block">
<faust-editor><!--

//--------------------------inlooper----------------------------
//
// # inlooper
//
// A simple looper that allows capture, playback, reversing, and looping with speed control from a mono input. 
// It wraps around when the end of the capture section is reached. 
// The loop start and end positions, as well as the playback direction can be controlled.
// A horizontal bar graph displays the current playback position within the loop.
//
// #### Usage
//
// 
// _ : inlooper(length) : _;
// 
// Where:
//
// * length: the loop duration in samples (constant numerical expression). 
//
//------------------------------------------------------------

import("stdfaust.lib");

declare name      "inlooper";
declare version   "1.0";
declare author    "Grame";
declare license   "BSD";
declare copyright "(c)GRAME 2025";

inlooper(length, x) = attach(rwtable(length, 0.0, rec_index, x, loop_index), pos)
with {

    // User controls
    speed = hslider("h:Controls/[3]Speed", 1, 0.2, 5, 0.01); // Slider to control playback speed
    record = button("h:Controls/[4]Record");                 // Button to start/stop recording
    play = checkbox("h:Controls/[5]Play/Stop");              // Checkbox to start/stop playback
    reverse = checkbox("h:Controls/[6]Reverse");             // Checkbox to play in reverse

    // Loop start and end positions with smoothing (scaled to the captured input length) 
    loop_start = length * hslider("[1]Loop_start", 0, 0, 1, 0.01) : si.smoo;
    loop_end = length * hslider("[2]Loop_end", 1, 0, 1, 0.01) : si.smoo;

    // Index counter that tracks recording position
    rec_index = (+(1) : %(length)) ~ *(record);

    // Current playback position within the loop
    loop_index = min_loop_pos + ma.modulo(index, max(ma.EPSILON, loop_length))
    with {

        // Minimum loop position
        min_loop_pos = min(loop_start, loop_end);

        // Loop duration
        loop_length = abs(loop_end - loop_start);

        // Playback speed with direction
        read_speed = speed * ba.if(reverse, -1, 1);

        // Index counter that tracks playback position
        index = windex
        letrec {
            'windex = windex + play * read_speed;
        };

    };

    // Display playback position as a horizontal bar graph
    pos = (loop_index / length) : hbargraph("[7]Position", 0, 1);

};

process = inlooper(MAX_LENGTH)
with {
    MAX_LENGTH = 5 * 44100; // 5 seconds at 44100 Hz, has to be a constant
};


--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="matrix">matrix</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust5/exfaust5.svg" class="mx-auto d-block">
<faust-editor><!--

declare name      "matrix";
declare version   "1.0";
declare author    "Grame";
declare license   "BSD";
declare copyright "(c)GRAME 2006";

//-----------------------------------------------
// Audio Matrix : N inputs x M outputs
//-----------------------------------------------

import("stdfaust.lib");

Fader(in)    = ba.db2linear(vslider("Input %in", -10, -96, 4, 0.1));
Mixer(N,out) = hgroup("Output %out", par(in, N, *(Fader(in)) ) :> _ );
Matrix(N,M)  = tgroup("Matrix %N x %M", par(in, N, _) <: par(out, M, Mixer(N, out)));

process = Matrix(8, 8);

--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="miditester">midiTester</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust6/exfaust6.svg" class="mx-auto d-block">
<faust-editor><!--

declare name      "midiTester";
declare version   "1.0";
declare author    "Vincent Rateau, GRAME";
declare license   "GPL v3";
declare reference "www.sonejo.net";

// FAUST MIDI TESTER

process = _*0, (vgroup("FAUST MIDI TESTER", hgroup("[1]", controltester, controlchantester, noteontester, noteonchantester, noteofftester, noteoffchantester, keypresschantester, midiclocktester), hgroup("[2]", kattester, katchantester, pctester, pcchantester, chattester, chatchantester, pitchwheeltester, pitchwheelchantester) :> _)) : attach;

///////////////////////////

// Ctrl tester (ctrl ): tester(midi in, midi out)
controltester = vgroup("CTRL IN/OUT", valuetest(50,51), booltest(100,101))
with {
    valuetest(i,o) = hslider("Ctrl Value IN (Ctrl %i) [midi:ctrl %i]", 60, 0, 127, 1) : hbargraph("Ctrl Value OUT (Ctrl %o) [midi:ctrl %o]", 0, 127);
    booltest(i,o) = checkbox("Ctrl Bool IN (Ctrl %i) [midi:ctrl %i]") : hbargraph("Ctrl Bool OUT (Ctrl %o) [midi:ctrl %o]", 0, 1);
};

// Ctrl Chan tester (ctrl chan): tester(midi in, midi out)
controlchantester = vgroup("CTRL CHAN IN/OUT", valuetest(50,2,74,3))
with {
    valuetest(i,ic,o,oc) = hslider("Ctrl Value IN (Ctrl %i Channel %ic) [midi:ctrl %i %ic]", 60, 0, 127, 1) : hbargraph("Ctrl Value OUT (Ctrl %o) Channel OUT(Chan %oc) [midi:ctrl %o %oc]", 0, 127);
};

// Note tester (keyon) : tester(midi in, midi out)
noteontester = vgroup("NOTE ON IN/OUT", valuetest(50,51), booltest(100,101))
with {
    valuetest(i,o) = hslider("NoteOn Value IN (Note %i) [midi:keyon %i]", 60, 0, 127, 1) : hbargraph("NoteOn Value OUT (Note %o) [midi:keyon %o]", 0, 127);
    booltest(i,o) = checkbox("NoteOn Bool IN (Note %i) [midi:keyon %i]") : hbargraph("NoteOn Bool OUT (Note %o) [midi:keyon %o]", 0, 1);
};

// Note Chan tester (keyon) : tester(midi in, midi out)
noteonchantester = vgroup("NOTE ON CHAN IN/OUT", valuetest(50, 2, 51, 3), booltest(50, 2, 101, 3))
with {
    valuetest(i, ic, o, oc) = hslider("NoteOn Value IN (Note %i Channel %ic) [midi:keyon %i %ic]", 60, 0, 127, 1) : hbargraph("NoteOn Value OUT (Note %o Chan %oc) [midi:keyon %o %oc]", 0, 127);
    booltest(i, ic, o, oc) = checkbox("NoteOn Chan Bool IN (Note %i Channel %ic) [midi:keyon %i %ic]") : hbargraph("NoteOn Chan Bool OUT (Note %o Channel %oc) [midi:keyon %o %oc]", 0, 1);
};

// Note tester (keyoff) : tester(midi in, midi out)
noteofftester = vgroup("NOTE OFF IN/OUT", valuetest(50,51), booltest(100,101))
with {
    valuetest(i,o) = hslider("NoteOff Value IN (Note %i) [midi:keyoff %i]", 60, 0, 127, 1) : hbargraph("NoteOff Value OUT (Note %o) [midi:keyoff %o]", 0, 127);
    booltest(i,o) = checkbox("NoteOff Bool IN (Note %i) [midi:keyoff %i]") : hbargraph("NoteOff Bool OUT (Note %o) [midi:keyoff %o]", 0, 1);
};

// Note Chan tester (keyoff) : tester(midi in, midi out)
noteoffchantester = vgroup("NOTE OFF CHAN IN/OUT", valuetest(50, 2, 51, 3), booltest(50, 2, 101, 3))
with {
    valuetest(i, ic, o, oc) = hslider("NoteOff Value IN (Note %i Channel %ic) [midi:keyoff %i %ic]", 60, 0, 127, 1) : hbargraph("NoteOff Value OUT (Note %o Channel %oc) [midi:keyoff %o %oc]", 0, 127);
    booltest(i, ic, o, oc) = checkbox("NoteOff Bool IN (Note %i Channel %ic) [midi:keyoff %i %ic]") : hbargraph("NoteOff Bool OUT (Note %o Channel %oc) [midi:keyoff %o %oc]", 0, 1);
};

// KeyPress Chan tester (keypress) : tester(midi in, midi out)
keypresschantester = vgroup("KEY PRESS CHAN IN/OUT", valuetest(50, 2, 51, 3), booltest(50, 2, 101, 3))
with {
    valuetest(i, ic, o, oc) = hslider("Pressure Value IN (Note %i Channel %ic) [midi:keypress %i %ic]", 60, 0, 127, 1) : hbargraph("Note Value OUT (Note %o Channel %oc) [midi:keypress %o %oc]", 0, 127);
    booltest(i, ic, o, oc) = checkbox("Pressure Bool IN (Note %i Channel %ic) [midi:keypress %i %ic]") : hbargraph("Pressure Bool OUT (Note %o Channel %oc) [midi:keypress %o %oc]", 0, 1);
};

// Midisync tester
midiclocktester = vgroup("MIDI SYNC (IN)", clock, startstop)
with {
    clock = checkbox("MIDI clock signal [midi:clock]");
    startstop = checkbox("MIDI START/STOP [midi:start] [midi:stop]");
};

// Key Aftertouch tester (keypress) : tester(midi in, midi out)
kattester = vgroup("KEY AFTERTOUCH (KAT) IN/OUT",valuetest(50,51), booltest(100,101))
with {
    valuetest(i,o) = hslider("Note KAT Value IN (Note %i) [midi:keypress %i]", 60, 0, 127, 1) : hbargraph("Note KAT Value OUT (Note %o) [midi:keypress %o]", 0, 127);
    booltest(i,o) = checkbox("Note KAT Bool IN (Note %i) [midi:keypress %i]") : hbargraph("Note KAT Bool OUT (Note %o) [midi:keypress %o]", 0, 1);
};

// Key Aftertouch tester (keypress) : tester(midi in, midi out)
katchantester = vgroup("KEY AFTERTOUCH CHAN (KAT) IN/OUT",valuetest(50,2,51,3), booltest(100,2,101,3))
with {
    valuetest(i,ic,o,oc) = hslider("Note KAT Value IN (Note %i) (Chan %ic) [midi:keypress %i %ic]", 60, 0, 127, 1) : hbargraph("Note KAT Value OUT (Note %o) (Chan %oc) [midi:keypress %o %oc]", 0, 127);
    booltest(i,ic,o,oc) = checkbox("Note KAT Bool IN (Note %i) (Chan %ic) [midi:keypress %i %ic]") : hbargraph("Note KAT Bool OUT (Note %o) (Chan %oc)[midi:keypress %o %oc]", 0, 1);
};

// ProgramChange tester (pgm) : tester(midi in, midi out)
pctester = vgroup("PROGRAM CHANGE (PC) IN/OUT",valuetest(1,2), booltest(1,2))
with {
    valuetest(i,o) = hslider("ProgramChange Value IN (PC %i) [midi:pgm %i]", 60, 0, 127, 1) : hbargraph("ProgramChange Value OUT (PC %o) [midi:pgm %o]", 0, 127);
    booltest(i,o) = checkbox("ProgramChange Bool IN (PC %i) [midi:pgm %i]") : hbargraph("ProgramChange Bool OUT (PC %o) [midi:pgm %o]", 0, 1);
};

// ProgramChange Chan tester (pgm) : tester(midi in, midi out)
pcchantester = vgroup("PROGRAM CHANGE CHAN (PC) IN/OUT",valuetest(1, 2, 2, 3), booltest(1, 2, 2, 3))
with {
    valuetest(i,ic,o,oc) = hslider("ProgramChange Value IN (PC %i) (CHAN %ic) [midi:pgm %i %ic]", 60, 0, 127, 1) : hbargraph("ProgramChange Value OUT (PC %o) (CHAN %oc) [midi:pgm %o %oc]", 0, 127);
    booltest(i,ic,o,oc) = checkbox("ProgramChange Bool IN (PC %i) (CHAN %ic) [midi:pgm %i %ic]") : hbargraph("ProgramChange Bool OUT (PC %o) (CHAN %oc) [midi:pgm %o %oc]", 0, 1);
};

// Channel Aftertourch tester (chanpress) : tester(midi in, midi out)
chattester = vgroup("CHANNEL AFTERTOUCH (CHAT) IN/OUT",valuetest(50,51), booltest(100,101))
with {
    valuetest(i,o) = hslider("Note CHAT Value IN (Note %i) [midi:chanpress %i]", 60, 0, 127, 1) : hbargraph("Note CHAT Value OUT (Note %o) [midi:chanpress %o]", 0, 127);
    booltest(i,o) = checkbox("Note CHAT Bool IN (Note %i) [midi:chanpress %i]") : hbargraph("Note CHAT Bool OUT (Note %o) [midi:chanpress %o]", 0, 1);
};

// Channel Aftertourch tester (chanpress) : tester(midi in, midi out)
chatchantester = vgroup("CHANNEL AFTERTOUCH CHAN (CHAT) IN/OUT",valuetest(50,2,51,3), booltest(100,2,101,3))
with {
    valuetest(i,ic,o,oc) = hslider("Note CHAT Chan Value IN (Note %i) (Chan %ic) [midi:chanpress %i %ic]", 60, 0, 127, 1) : hbargraph("Note CHAT Value OUT (Note %o) (Chan %oc) [midi:chanpress %o %oc]", 0, 127);
    booltest(i,ic,o,oc) = checkbox("Note CHAT Bool IN (Note %i) (Chan %ic) [midi:chanpress %i %ic]") : hbargraph("Note CHAT Bool OUT (Note %o) (Chan %oc) [midi:chanpress %o %oc]", 0, 1);
};

// Pitchwheel tester (pitchwheel) : tester(midi in, midi out)
pitchwheeltester = vgroup("PITCHWHEEL IN/OUT",valuetest, booltest)
with {
    valuetest = hslider("Pitchwheel Value IN  [midi:pitchwheel]", 0, -8192, 8191, 1) : hbargraph("Pitchwheel Value OUT[midi:pitchwheel]", -8192, 8191);
    booltest = checkbox("Pitchwheel Bool IN [midi:pitchwheel]") : hbargraph("Pitchwheel Bool OUT [midi:pitchwheel]", 0, 1);
};

// Pitchwheel Chan tester (pitchwheel) : tester(midi in, midi out)
pitchwheelchantester = vgroup("PITCHWHEEL CHAN IN/OUT",valuetest(2, 15), booltest(2, 15))
with {
    valuetest(ic, oc) = hslider("Pitchwheel Value IN (Chan %ic) [midi:pitchwheel %ic]", 0, -8192, 8191, 1) : hbargraph("Pitchwheel Value OUT (Chan %oc)[midi:pitchwheel %oc]", -8192, 8191);
    booltest(ic, oc) = checkbox("Pitchwheel Bool IN (Chan %ic) [midi:pitchwheel %ic]") : hbargraph("Pitchwheel Bool OUT (Chan %oc) [midi:pitchwheel %oc]", 0, 1);
};

--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="paradigma">paradigma</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust7/exfaust7.svg" class="mx-auto d-block">
<faust-editor><!--

// Modulation synthesis with sparse convolution filter and distortions.
// ---------------------------------------------------------------------
//
// A "3D" oscillator oscillating on x,y,z axis, with the radius of x,y used as waveform,
// very similar and related to FM / AM.
//
// The y axis oscillation is set by MIDI pitch, x and z are detuned by simple just tuned ratios.
// Feedback acts on the individual sine oscillations (giving a sawtooth like waveform).
//
// Three weighted copies with time varying shifts are summed in a lossy integrator
// (sparse convolution), followed by a peak resonance filter and shaped by
// an internal pick-up like distortion and an asymmetric polynomial.
//
// The convolution tabs give a (variyng) triangle impulse response if integrated twice,
// with a -12 dB/octave rolloff and regular notches.
// Here only one integrator is used.
//
// The envelope is hard wired to the oscillation amplitudes and the rise time of the filter.
//
// An LFO is wired to pitch.
//
// A resonant EQ and the Dattoro Reverb from the Faust libary are added as effect on the sum.
//
// Inspired by the history of sound synthesis, namely Trautonium, Mini Moog, Phase Modulation Synthesis,
// Variophon Wind Instrument Synthesizer, Physical Modeling, and the work of Thomas D. Rossing.
//
// References:
// Kot, Vitezslav. (2006). DIGITAL SOUND EFFECTS ECHO AND REVERB BASED ON NON EXPONENTIALLY DECAYING COMB FILTER.
// https://en.wikipedia.org/wiki/Variophon
// Parker, Julian & Zavalishin, Vadim & Le Bivic, Efflam. (2016). Reducing The Aliasing Of Nonlinear Waveshaping Using Continuous-TimeConvolution.
// Nicholas G. Horton, Thomas R. Moore. (2008). Modelling The Magnetic Pickup Of An Electric Guitar.
// https://www.musicdsp.org/en/latest/Effects/86-waveshaper-gloubi-boulga.html, see comment from 2005-09-22 01:07:58
// Frei, Beat. Digital Sound Generation I & II, ICST Zurich University of the Arts
// Smith, J.O. Physical Audio Signal Processing,http://ccrma.stanford.edu/~jos/pasp/, online book, 2010 edition

declare options "[midi:on][nvoices:8]";
declare name "Paradigma_9";
declare version "1.0";
declare author "gabriel";

import("stdfaust.lib");

// Frequency Ratios table
frtonum = waveform{1, 16, 9, 6, 5, 4, 7, 3, 8, 5, 7, 15};
frtodiv = waveform{1, 15, 8, 5, 4, 3, 5, 2, 5, 3, 4, 8};

// MIDI
// minimum velocity
minvelo = 1/32;
midigrp(x) = hgroup("[1]MIDI", x);
f = nentry("freq[hidden:1]", 200, 40, 2000, 0.1);
kmidi = nentry("key[hidden:1]", 69, 0, 127, 1);
bend = ba.semi2ratio(hslider("bend[hidden:1][midi:pitchwheel][style:
knob]", 0, -2, 2, 0.01));
gain = nentry("gain[hidden:1]", 0.6, 0, 1, 0.01)<:*:_*(1-minvelo):_+minvelo;
master = hslider("volume[midi:ctrl 7]", 0.6, 0, 1, 0.01);
gate = button("gate[hidden:1]");

// Oscillator Parameter
rtogrp(x) = hgroup("[2]Oscillator", x);
rto1sel = rtogrp(hslider("[1]x[style:knob]", -12, -36, 36, 1));
rto2sel = rtogrp(hslider("[2]z[style:knob]", 19, -36, 36, 1));
fbka = rtogrp(hslider("[3]Feedback[style:knob]", 0.15, 0, 1, 0.01)<:*:*(1/ma.PI));
detune = rtogrp(hslider("[4]Detune[style:knob]", 0.125, 0, 0.5, 0.005)/ma.SR);

// LFO and Envelope Parameter
lfogrp(x) = hgroup("[3]Envelope & LFO", x);
enva = (lfogrp(ba.db2linear(hslider("[1]A[style:knob]", 20, 15, 66, 1))/1000));
envd = (lfogrp(ba.db2linear(hslider("[2]D[style:knob]", 74, 26, 100, 1))/1000)*envpscal);
envs = (lfogrp(hslider("[3]S[style:knob]", 0, 0, 1, 0.01)));
envr = (lfogrp(ba.db2linear(hslider("[4]R[style:knob]", 50, 26, 100, 1))/1000)*envpscal);
lfof = lfogrp(hslider("[5]LFO Hz[style:knob]", 3, 0.1, 12, 0.1));
lfvibra = lfogrp(hslider("[6]Vibrato[style:knob]", 0.125, 0, 1, 0.01))<:*;

env = en.adsre(enva, envd*envpscal, envs, envr*envpscal, gate);
envg = env:_*gain;

lfosn = qsin(mphasor(lfof/ma.SR));

// Triangular Filter Parameter
fltgrp(x) = hgroup("[4]Filter", x);
wid = fltgrp(hslider("[1]Rise[style:knob]", 3, 1, 9, 0.001)):2^_:1/_;
edge = fltgrp(hslider("[2]Fall[style:knob]", 6, 1, 9, 0.001)):2^_:1/_;
fiq = fltgrp(hslider("[3]Q[style:knob]", 1, 0.5, 3.87, 0.01))<:*;
drive = fltgrp(hslider("[4]Drive[style:knob]", -12, -12, 30, 0.1)):_/20.0:10^_;

// Modulation Frequency Ratios
rto1oct = rto1sel/12:floor;
rto1semi = rto1sel+36:_%12;
rto1a = frtonum, rto1semi:rdtable;
rto1b = frtodiv, rto1semi:rdtable;
rto1 = (rto1a/rto1b)*(2^rto1oct);
rto1r = min((1/rto1), 1);

rto2oct = rto2sel/12:floor;
rto2semi = rto2sel+36:_%12;
rto2a = frtonum, rto2semi:rdtable;
rto2b = frtodiv, rto2semi:rdtable;
rto2 = rto1*(rto2a/rto2b)*(2^rto2oct);
rto2r = min((1/rto2), 1);

// Pitch
lg2f = ma.log2(f/440);
stretch = 0.0333*lg2f;
envpscal = (-3*lg2f):ba.db2linear;
fplus = f*bend+lfosn*lfvibra*f*0.5/12*envg+stretch;

w = f/ma.SR;
w2 = rto1*w;
w3 = rto2*w;
wplus = fplus/ma.SR;

fbk1 = fbka*(0.5-w)^4;
fbk2 = fbka*(0.5-w2)^4*rto1r;
fbk3 = fbka*(0.5-w3)^4*rto2r;

// Modulation Reduction Per Frequency
redux1 = ((3.3-((rto1+1)*w))/3.3), 0:max:_^3;
redux2 = ((3.3-((rto2+1)*w))/3.3), 0:max:_^3;
modep = envg;
modep1 = envg*redux1*rto1r*gain;
modep2 = envg*redux2*rto2r*gain;

// Sine Oscillator
wrap(n) = n-(floor(n+0.5));
// Bhaskara I based approximate sine curve
qsincurve(x) = 1-((x*x)<:*(1.2253517*16), (_<:*:*(-3.60562732*16)):>_);
qsin(x) = x+(0.5):wrap<:(abs:-(0.25):qsincurve), _:ma.copysign;
// Feedback Depth Reduction Curve
fbcurve(x) = x:abs:-(1)<:^(3):_, (x):ma.copysign;

// Oscillator
mphasor(fw) = (+(fw)~(wrap));
oscsn(fw, off) = mphasor(fw)+off:qsin:+~*(0.5);
osc1(fw, off) = ((fw), +(off):(oscsn))~(*(fbk2):fi.pole(0.5):_*fbcurve(fw));

// 3D to 2D radius
oscy(fw, off) = (osc1(fw, off)*osc1(fw*rto2+2*detune, 0.75+off)*modep2)*modep;
oscx(fw, off) = (osc1(fw*rto1+detune,
    0.25+off)*osc1(fw*rto2+2*detune, 0.25+off)*modep2)*modep1;
oscxy(fw, off) = (oscy(fw, off)<:*), (oscx(fw, off)<:*):+:sqrt;

// Pick-Up like Distortion
// distance :
pickd = 0.25;
pickup(x, pickd) = x, // normal for in < 1.2e-4
(x, (x^2:_+pickd:_^(3/2)):/), // ILO:
(pickd^(3/2)/(sqrt(x*x+1)):ma.neg:_+pickd^(3/2)):// select
ba.if((_:abs:_<=1.2e-4), _, _):_*(pickd^(4/3));

// Basic Synthvoice, modulated Oscillations
synthvox(fw, ph2, ph3, g1, g2, g3) = (oscxy(fw, 0):_*g1), (oscxy(fw,
    ph2):_*g2), (oscxy(fw, ph3):_*g3):>_:fi.zero(1.0)<:_, (pickd):pickup;

// Triangle
// reduce width with frequency
widredux = w<:+:_^3:1.0-_;
// diff to max f in octaves, reduced for higher octaves
dwo = (0.25/wid):max(_, 1):ma.log2:ma.inv:_*widredux:ma.inv;
// falling edge
egderto = edge/wid;
wid2 = wid*(2^(dwo*(1-envg))):_*(2^(dwo*(1-gain))):min(_, 0.25):max(_, 4/(ma.SR/fplus));
wid2e = edge:min(_, 0.25):max(_, 4/(ma.SR/fplus));

fiw = wplus/wid2;
fiwtail = wplus/wid2e;
// triangle coefficients
apg0 = fiw;
apg1 = -apg0-fiwtail;
apg2 = fiwtail;
// integration freq
igpole = 1.0-5.0/ma.SR;
resf = (fplus/(wid2+wid2e)):min(_, (0.249*ma.SR));

// Asymmetric Shaper x - 0.15x^2-0.15x^3
tubicclip = _:min(_, (1.19419)):max(_, (-1.86086));
tubicilo(x) = x, // normal for in < 1.2e-4
(x-0.15*(x^2)-0.15*(x^3)), // ILO:
((0.5*(x^2)-0.05*(x^3)-0.0375*(x^4)), (x<:_, _':-:_<:(abs:max(_, 1.2e-4)), (ma.signum):ma.copysign):/):// select
ba.if((_:abs:_<=1.2e-4), _, _):fi.dcblockerat(10.0);

// Sound
process = synthvox(wplus, wid2, wid2e, apg0, apg1, apg2):fi.dcblockerat(10.0):fi.pole(igpole):fi.svf.peak(resf, fiq):_*drive:tubicclip:tubicilo:_*(1/drive);
effect = _*master:preeq(lsf,
    lsgain,
    b1f,
    eqq2,
    eqg2,
    b2f,
    eqq3,
    eqg3,
    hsf,
    hsgain)<:_, _:re.dattorro_rev_default;

// ---------------------------------------------------------------------------------------------------------------
// Resonant EQ for instrument corpus, based on SVF
eqgrp(x) = hgroup("[5]EQ", x);
lsgain = eqgrp(hslider("[1]Low Gain[style:knob]", 3, -18, 18, 0.25));
b1f = eqgrp(hslider("[2]Split F Low[style:knob]", -0.5, -1, 1, 0.05)):(2.0)^_:_*360;
b1gain = eqgrp(hslider("[3]Band 1 Gain[style:knob]", 4.5, -18, 18, 0.25));
b2f = eqgrp(hslider("[4]Split F Hi[style:knob]", -0.5, -1, 1, 0.05)):(2.0)^_:_*720;
b2gain = eqgrp(hslider("[5]Band 2 Gain[style:knob]", 4.5, -18, 18, 0.25));
hsgain = eqgrp(hslider("[6]Hi Gain[style:knob]", -3, -18, 18, 0.25));

// Q and gain of middle bands are scaled simultanously, with saturation curveongain,
// max gain is reduced from the output. Bands are spaced in octaves by default.
lsf = b1f*0.5;
hsf = b2f*2;
gcurve(gain, gainrange) = abs(gain/gainrange)<:*:1-_:_+1:_*0.5;
qscal(gain, gainrange) = 1.414*(abs(gain/gainrange)):_+1.414;
eqq2 = qscal(b1gain, 18.0);
eqg2 = b1gain*gcurve(b1gain, 18.0);
eqq3 = qscal(b2gain, 18.0);
eqg3 = b2gain*gcurve(b2gain, 18.0);
eqredux = max(lsgain, eqg2):max(_, eqg3):max(_, hsgain):ba.db2linear:ma.inv;

preeq(f1, g1, f2, q2, g2, f3, q3, g3, f4, g4) = _*eqredux:fi.svf.bell(f1,
    1.414,
    g1):fi.svf.bell(f2, q2, g2):fi.svf.bell(f3,
    q3,
    g3):fi.svf.hs(f4, 1.414, g4);

--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="sflooper">sflooper</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust8/exfaust8.svg" class="mx-auto d-block">
<faust-editor><!--

//--------------------------sflooper----------------------------
//
// # sflooper
//
// A simple looper that allows playback, reversing, and looping with speed control within a sound file. 
// It automatically adapts to the number of channels in the input sound file and wraps around 
// when the end of the sound file is reached. 
// The loop start and end positions, as well as the playback direction can be controlled.
// A horizontal bar graph displays the current playback position within the loop.
//
// #### Usage
//
// 
// sflooper(sound) : si.bus(outputs(sound)-2);
// 
// Where:
//
// * sound: the input sound file.
//
// Example:
//
// 
// sound = soundfile("sound[url:{'loop.wav'}]",2);
// sflooper(sound);
// 
//------------------------------------------------------------

import("stdfaust.lib");

declare name      "looper";
declare version   "1.0";
declare author    "Grame";
declare license   "BSD";
declare copyright "(c)GRAME 2025";

// Play button state is used to control loop_reader index increment and actual output signal generation.

sflooper(sf) = (0, play * loop_index) : sf : !, !, attach(_,pos), si.bus(outputs(sf)-3)
with {

    // User controls
    speed = hslider("h:Controls/[3]Speed", 1, 0.2, 5, 0.01); // Slider to control playback speed
    play = checkbox("h:Controls/[4]Play/Stop");              // Checkbox to start/stop playback
    reverse = checkbox("h:Controls/[5]Reverse");             // Checkbox to play in reverse

    // Loop start and end positions with smoothing (scaled to the sound file length) 
    loop_start = length * hslider("[1]Loop_start", 0, 0, 1, 0.01) : si.smoo;
    loop_end = length * hslider("[2]Loop_end", 1, 0, 1, 0.01) : si.smoo;

    // Sound file length
    length = (0, 0) : sf : (_, si.block(outputs(sf) - 1));  

    // Current playback position within the loop
    loop_index = min_loop_pos + ma.modulo(index, max(ma.EPSILON, loop_length))
    with {

        // Minimum loop position
        min_loop_pos = min(loop_start, loop_end);

        // Loop duration
        loop_length = abs(loop_end - loop_start);

        // Playback speed with direction
        read_speed = speed * ba.if(reverse, -1, 1);

        // Index counter that tracks playback position
        index = windex
        letrec {
            'windex = windex + play * read_speed;
        };

    };

    // Display playback position as a horizontal bar graph
    pos = (loop_index / length) : hbargraph("[6]Position", 0, 1);

};

declare soundfiles "https://raw.githubusercontent.com/sletz/faust-sampler/main";
sound = soundfile("sound[url:{'violon.wav'}]", 2);

process = sflooper(sound);


--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="statespace">statespace</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust9/exfaust9.svg" class="mx-auto d-block">
<faust-editor><!--

declare name        "statespace";
declare version     "1.0";
declare author      "JOS";
declare license     "MIT";
declare copyright   "(c) Julius O. Smith III, 2020";

//-----------------------------------------------
// General Linear State-Space Model Example
//-----------------------------------------------

import("stdfaust.lib");

p = 2; // number of inputs
q = 3; // number of outputs
N = 5; // number of states

A = matrix(N,N); // state transition matrix
B = matrix(N,p); // input-to-states matrix
C = matrix(q,N); // states-to-output matrix
D = matrix(q,p); // direct-term matrix, bypassing state

// ./matrix.dsp with M and N transposed to follow convention:
matrix(M,N) = tgroup("Matrix: %M x %N", par(in, N, _)
              <: par(out, M, mixer(N, out))) with {
  fader(in) = ba.db2linear(vslider("Input %in", -10, -96, 4, 0.1));
  mixer(N,out) = hgroup("Output %out", par(in, N, *(fader(in)) ) :> _ );
};

Bd = par(i,p,mem) : B; // input delay needed for conventional definition
vsum(N) = si.bus(2*N) :> si.bus(N); // vector sum of two N-vectors

// Illustrate nonzero initial state, following conventional definition:
impulse = 1-1'; // For zero initial state, set impulse = 0 or simplify code
x0 = par(i,N,i*impulse); // initial state = (0,1,2,3,...,N-1)

system = si.bus(p) <: D, (Bd : vsum(N)~(A), x0 : vsum(N) : C) :> si.bus(q);

process = system;


--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="switcher">switcher</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust10/exfaust10.svg" class="mx-auto d-block">
<faust-editor><!--

declare name        "switcher";
declare version     "1.0";
declare author      "Grame";
declare license     "BSD";
declare copyright   "(c)GRAME 2007";

//-----------------------------------------------
// Switch between two stereo sources. 
// Useful to compare these two sources
// The parameter c\in{0,1} indicates the 
// channels to select
//-----------------------------------------------

switch(c,x0,x1,y0,y1) = sel(c,x0,y0), sel(c,x1,y1)
                        with { 
                            sel(c,x,y) = (1-c)*x + c*y; 
                        };

process = switch(hslider("source 0 <-> source 1",0,0,1,1));


--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="tester">tester</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust11/exfaust11.svg" class="mx-auto d-block">
<faust-editor><!--

declare name      "tester";
declare version   "1.0";
declare author    "Grame";
declare license   "BSD";
declare copyright "(c)GRAME 2006";

//-----------------------------------------------
// Tester : tests louspeakers
// Send a test signal( sine, noise, pink) to one 
// of 8 loudspeakers
//-----------------------------------------------

import("stdfaust.lib");

// TODO: this should be rewritten with the pink noise function of noises.lib
pink = f : (+ ~ g) with {
    f(x) = 0.04957526213389*x - 0.06305581334498*x' + 0.01483220320740*x'';
    g(x) = 1.80116083982126*x - 0.80257737639225*x';
};

// User interface
//----------------
vol  = hslider("[2] volume [unit:dB]", -96, -96, 0, 1): ba.db2linear : si.smoo;
freq = hslider("[1] freq [unit:Hz]", 1000, 10, 20000, 1);
dest = hslider("[3] destination", 0, 0, 8, 1);

testsignal = os.osci(freq)*checkbox("sine wave")
            + no.noise * checkbox("white noise")
            + pink(no.noise) * ba.db2linear(20)  * checkbox("pink noise");

process = vgroup("Audio Tester", testsignal*vol <: par(i, 8, *(dest==i)));


--></faust-editor>
<br>
</div>
<!-- /faust-run -->

<h2 id="tester2">tester2</h2>
<!-- faust-run -->
<div class="faust-run"><img src="exfaust12/exfaust12.svg" class="mx-auto d-block">
<faust-editor><!--

declare name      "tester2";
declare version   "1.0";
declare author    "Grame";
declare license   "BSD";
declare copyright "(c)GRAME 2014";

//-----------------------------------------------
// Stereo Audio Tester : send a test signal (sine, 
// noise, pink) on a stereo channel
//-----------------------------------------------

import("stdfaust.lib");

pink = f : (+ ~ g) with {
    f(x) = 0.04957526213389*x - 0.06305581334498*x' + 0.01483220320740*x'';
    g(x) = 1.80116083982126*x - 0.80257737639225*x';
};

// User interface
//----------------
transition(n) = \(old,new).(ba.if(old<new, min(old+1.0/n,new), max(old-1.0/n,new))) ~ _;

vol  = hslider("[2] volume [unit:dB]", -96, -96, 0, 1): ba.db2linear : si.smoo;
freq = hslider("[1] freq [unit:Hz][scale:log]", 440, 40, 20000, 1);
wave = nentry("[3] signal [style:menu{'white noise':0;'pink noise':1;'sine':2}]", 0, 0, 2, 1) : int;
dest = nentry("[4] channel [style:radio{'none':0;'left':1;'right':2;'both':3}]", 0, 0, 3, 1) : int;

testsignal = no.noise, pink(no.noise), os.osci(freq): select3(wave);

process = vgroup("Stereo Audio Tester", testsignal*vol <: par(i, 2, *((dest & (i+1)) != 0 : transition(4410))));


--></faust-editor>
<br>
</div>
<!-- /faust-run --></div>
        </div>
    </div>

    <footer class="col-md-12">
        <hr>
        <p>Copyright &copy; 2020-2025 <a href="https://www.grame.fr">Grame-CNCM</a></p>
    </footer>
    <script>
        var base_url = "../..",
        shortcuts = {"help": 191, "next": 78, "previous": 80, "search": 83};
    </script>
    <script src="../../js/base.js" defer></script>
    <script src="/js/faust-web-component.js" defer></script>
    <script src="../../search/main.js" defer></script>

    <div class="modal" id="mkdocs_search_modal" tabindex="-1" role="dialog" aria-labelledby="searchModalLabel" aria-hidden="true">
    <div class="modal-dialog modal-lg">
        <div class="modal-content">
            <div class="modal-header">
                <h4 class="modal-title" id="searchModalLabel">Search</h4>
                <button type="button" class="close" data-dismiss="modal"><span aria-hidden="true">&times;</span><span class="sr-only">Close</span></button>
            </div>
            <div class="modal-body">
                <p>From here you can search these documents. Enter your search terms below.</p>
                <form>
                    <div class="form-group">
                        <input type="search" class="form-control" placeholder="Search..." id="mkdocs-search-query" title="Type search term here">
                    </div>
                </form>
                <div id="mkdocs-search-results" data-no-results-text="No results found"></div>
            </div>
            <div class="modal-footer">
            </div>
        </div>
    </div>
</div><div class="modal" id="mkdocs_keyboard_modal" tabindex="-1" role="dialog" aria-labelledby="keyboardModalLabel" aria-hidden="true">
    <div class="modal-dialog">
        <div class="modal-content">
            <div class="modal-header">
                <h4 class="modal-title" id="keyboardModalLabel">Keyboard Shortcuts</h4>
                <button type="button" class="close" data-dismiss="modal"><span aria-hidden="true">&times;</span><span class="sr-only">Close</span></button>
            </div>
            <div class="modal-body">
              <table class="table">
                <thead>
                  <tr>
                    <th style="width: 20%;">Keys</th>
                    <th>Action</th>
                  </tr>
                </thead>
                <tbody>
                  <tr>
                    <td class="help shortcut"><kbd>?</kbd></td>
                    <td>Open this help</td>
                  </tr>
                  <tr>
                    <td class="next shortcut"><kbd>n</kbd></td>
                    <td>Next page</td>
                  </tr>
                  <tr>
                    <td class="prev shortcut"><kbd>p</kbd></td>
                    <td>Previous page</td>
                  </tr>
                  <tr>
                    <td class="search shortcut"><kbd>s</kbd></td>
                    <td>Search</td>
                  </tr>
                </tbody>
              </table>
            </div>
            <div class="modal-footer">
            </div>
        </div>
    </div>
</div>
    </body>

</html>
